// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zeroedX); // if zx == 0, zero the x input
    Not16(in=zeroedX, out=notZeroedX);
    Mux16(a=zeroedX, b=notZeroedX, sel=nx, out=notX); // if nx == 1, negate the x input
    
    Mux16(a=y, b=false, sel=zy, out=zeroedY); // if zy == 0, zero the y input
    Not16(in=zeroedY, out=notZeroedY);
    Mux16(a=zeroedY, b=notZeroedY, sel=ny, out=notY); // if ny == 1, negate the y input

    Add16(a=notX, b=notY, out=xPlusY);
    And16(a=notX, b=notY, out=xAndY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=plusOrAnd); // compute f -> 1 = x plus, 0 = x && y

    Not16(in=plusOrAnd, out=notPlusOrAnd);
    Mux16(a=plusOrAnd, b=notPlusOrAnd, sel=no, out=out, 
    out[0..7] = out07, out[8..15] = out815, out[15] = outMSB); 
    // if no == 1, negate output and send to out
    // Split out into two 8-bit busses and an MSB for ng and zr

    Or8Way(in=out07, out=or07);         // Check for 0s in bits 0 to 7
    Or8Way(in=out815, out=or815);       // Check for 0s in bits 8 to 15
    Or(a=or07, b=or815, out=allZeros);
    Not(in=allZeros, out=zr);           // Return zr
    And(a=outMSB, b=true, out=ng);

}